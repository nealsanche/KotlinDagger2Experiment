plugins {
    id 'com.gladed.androidgitversion' version '0.4.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

apply plugin: 'kotlin-allopen'

allOpen {
    annotation("com.robotsandpencils.kotlindaggerexperiment.OpenClass")
}

androidGitVersion {
    codeFormat 'MNNPPP'
    format '%tag%%-count%%-commit%%-branch%'
    hideBranches = [ 'develop', 'master' ]
}

def vName = androidGitVersion.name()
def vCode = androidGitVersion.code()

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.robotsandpencils.kotlindaggerexperiement"
        minSdkVersion 21
        targetSdkVersion 27

        // See: https://github.com/gladed/gradle-android-git-version.
        versionName vName
        versionCode vCode

        testInstrumentationRunner "com.robotsandpencils.kotlindaggerexperiement.TestRunner"

        // Output kotlin-dagger-experiment-flavor.apk instead of app-flavor.apk
        archivesBaseName = "kotlin-dagger-experiment"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

ext {
    supportVersion = "27.0.2"
    archVersion = "1.1.0"
    archVersionA = "1.0.0"
    daggerVersion = "2.14.1"
    ankoVersion = "0.10.1"
    groupieVersion = "2.0.0"
    mockitoVersion = "2.15.0"
    espressoVersion = "3.0.1"
    okhttpVersion = "3.9.1"
}

configurations.all {
    resolutionStrategy.force "com.android.support:support-annotations:$supportVersion"
    resolutionStrategy.force "com.android.support:support-v13:$supportVersion"
    resolutionStrategy.force "com.android.support:support-fragment:$supportVersion"
    resolutionStrategy.force "com.android.support:preference-v14:$supportVersion"
    resolutionStrategy.force "com.google.dagger:dagger:$daggerVersion"
    resolutionStrategy.force "com.squareup.okhttp3:okhttp:$okhttpVersion"
    resolutionStrategy.force "junit:junit:4.12"
    resolutionStrategy.force "com.google.code.findbugs:jsr305:3.0.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:customtabs:$supportVersion"
    implementation "com.android.support:cardview-v7:$supportVersion"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0-beta5'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:0.18"

    // Dagger 2 and Dagger Android
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // RXJava
    implementation "io.reactivex.rxjava2:rxjava:2.1.7"
    implementation "io.reactivex.rxjava2:rxkotlin:2.2.0"
    implementation "io.reactivex.rxjava2:rxandroid:2.0.1"
    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'
    implementation 'com.mtramin:rxfingerprint:2.1.0'

    // Architecture Components
    implementation "android.arch.lifecycle:runtime:$archVersion"
    implementation "android.arch.lifecycle:extensions:$archVersion"
    implementation "android.arch.persistence.room:runtime:$archVersionA"
    implementation "android.arch.persistence.room:rxjava2:$archVersionA"
    kapt "android.arch.lifecycle:compiler:$archVersionA"
    kapt "android.arch.persistence.room:compiler:$archVersionA"
    testImplementation "android.arch.persistence.room:testing:$archVersionA"

    // Groupie
    implementation "com.xwray:groupie:$groupieVersion"

    // Logging
    implementation 'com.github.ajalt:timberkt:1.3.0'

    // Secure preferences
    implementation 'com.scottyab:secure-preferences-lib:0.1.4'

    // Retrofit 2
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"


    // Image loading
    implementation 'com.squareup.picasso:picasso:2.5.2'

    // KTX
    implementation 'androidx.core:core-ktx:0.1'

    // Material Dialogs
    implementation 'com.afollestad.material-dialogs:core:0.9.4.2'
    implementation 'com.afollestad.material-dialogs:commons:0.9.4.2'

    // Android Tests
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$espressoVersion"

    androidTestImplementation("com.squareup.okhttp3:mockwebserver:$okhttpVersion") {
        exclude group: 'org.bouncycastle'
    }
    androidTestImplementation 'com.squareup.rx.idler:rx2-idler:0.9.0'
    androidTestImplementation('com.nhaarman:mockito-kotlin-kt1.1:1.5.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"

    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$daggerVersion"

    androidTestImplementation 'com.jakewharton.espresso:okhttp3-idling-resource:1.0.0'
    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
    //androidTestImplementation 'com.github.tmurakami:dexopener:0.12.1'

    // Unit Tests
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation('com.nhaarman:mockito-kotlin-kt1.1:1.5.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    testImplementation "android.arch.core:core-testing:$archVersionA"
}
